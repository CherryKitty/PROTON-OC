to-report o1
  ; number of current OC members
  report count all-persons with [ oc-member? ]
end

to-report o2
  ; Number of recruited individuals into OC per time unit
  report count all-persons with [ new-recruit = (ticks - 1) ]
end

to-report o3
  ; Number of crimes per time unit
  report sum [ num-crimes-committed-this-tick ] of all-persons
end

to-report o4
  ; Number of crimes committed by OC members per time unit
  report sum [ num-crimes-committed-this-tick ] of all-persons with [ oc-member? ]
end

to-report o5
  ; Distribution of C (which is the individual propensity towards crime commission)
  report make-histo [ -> criminal-tendency ] 7 0 1
end

to-report make-histo [ citizen-reporter bins min-v max-v ]
  let values [ runresult citizen-reporter ] of all-persons
  let counts n-values bins [ 0 ]
  assert [ -> min-v < max-v ]
  let thresholds (sentence (range min-v max-v ((max-v - min-v) / bins)) max-v) 
  let i 0 
  foreach values [ v ->
    assert [ -> v >= first thresholds and v <= last thresholds ]
    set i 0
    while [ v > item (i + 1) thresholds ] [ set i i + 1 ]
    set counts replace-item i counts (item i counts + 1)
  ]
  report (map [ [ a b ] -> (list a  b) ] but-last thresholds counts)
end

to-report make-discrete-histo [ citizen-reporter low high ]
  let values table:to-list table:counts runresult citizen-reporter
  let counts n-values (high - low + 1) [ i -> list (i + low) 0 ]
  foreach values [ v ->
    set counts replace-item ((first v) - low) counts (list first v last v) 
  ]
  report counts
end

to-report o5a
  ; average of C (which is the individual propensity towards crime commission)
  report mean [ criminal-tendency ] of all-persons
end

to-report o6
  ; Distribution of R (which is the individual embededdness into OC-prone local networks)
  report make-histo [ -> oc-embeddedness ] 7 0 1
end

to-report o6a
  ; average of R (which is the individual embededdness into OC-prone local networks)
  report mean [ oc-embeddedness ] of all-persons
end

to-report o7a
  ; Distribution of socio-demographic variables on new recruited individuals
  report make-discrete-histo [ -> [ education-level ] of all-persons with [ new-recruit = (ticks - 1) ] ] -1 4
end

to-report o7b
  ; Distribution of socio-demographic variables on new recruited individuals
report make-discrete-histo [ -> [ wealth-level ] of all-persons with [ new-recruit = (ticks - 1) ] ] 1 5
  
end

to-report o7c
  ; Distribution of socio-demographic variables on new recruited individuals
  report make-discrete-histo [ -> [ job-level ] of all-persons with [ new-recruit = (ticks - 1) ] ] 1 4
end

to-report o8a
  ; Distribution of socio-demographic variables of OC members
  report make-discrete-histo [ -> [ education-level ] of all-persons with [ oc-member? ] ] -1 4
end

to-report o8b
  ; Distribution of socio-demographic variables of OC members
  report make-discrete-histo [ -> [ wealth-level ] of all-persons with [ oc-member? ] ] 1 5
end

to-report o8c
  ; Distribution of socio-demographic variables of OC members
  report make-discrete-histo [ -> [ job-level ] of all-persons with [ oc-member? ] ] 1 4
end

to-report o9a
  ; Distribution of socio-demographic variables of “ordinary criminals” (people that commit crime but are not part of an OC)
  report make-discrete-histo [ -> [ education-level ] of all-persons with [ not oc-member? and num-crimes-committed > 0 ] ] -1 4
end

to-report o9b
  ; Distribution of socio-demographic variables of “ordinary criminals” (people that commit crime but are not part of an OC)
  report make-discrete-histo [ -> [ wealth-level ] of all-persons with [ not oc-member? and num-crimes-committed > 0 ] ] 1 5
end

to-report o9c
  ; Distribution of socio-demographic variables of “ordinary criminals” (people that commit crime but are not part of an OC)
  report make-discrete-histo [ -> [ job-level ] of all-persons with [ not oc-member?  and num-crimes-committed > 0 ] ] 1 4
end

to-report o10a
  ; Distribution of socio-demographic variables of the general population
  report make-discrete-histo [ -> [ education-level ] of all-persons ] -1 4
end

to-report o10b
  ; Distribution of socio-demographic variables of the general population
  report make-discrete-histo [ -> [ wealth-level ] of all-persons ] 1 5
end

to-report o10c
  ; Distribution of socio-demographic variables of the general population
  report make-discrete-histo [ -> [ job-level ] of all-persons ] 1 4
end

to-report o11
  ; Number of crimes committed by facilitator agents per time unit
  report sum [ num-crimes-committed ] of all-persons with [ facilitator? ]
end

to-report o12
  ; Number of law enforcement interventions per time unit
  report number-law-interventions-this-tick
end

to-report o13
  ;age structure of offenses
  let age-bins [ 0 13 17 24 34 44 54 64 200 ] ; same as in file crime_rate_by_gender_and_age_range
  report  map [ i -> 
    sum [ num-crimes-committed-this-tick ] of persons with [ age > item i age-bins and age <= item (i + 1) age-bins ] 
  ] but-last range length age-bins 
end

to-report o14
  ;co-offenses per turn. A vector of co-offenses the side of the index, starting with zero.
  report co-offender-group-histo 
end

to-report o15 
  ;number of persons addressed by a preventive interventions
  report number-protected-recruited-this-tick
end

to-report o16
  ;number of OC person offsprings recruited this turn
  report number-offspring-recruited-this-tick
end